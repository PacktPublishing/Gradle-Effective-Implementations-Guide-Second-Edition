subprojects {
    apply plugin: 'java'

    repositories {
        mavenCentral()
    }

    dependencies {
        testCompile 'junit:junit:4.8.2'
    }
}

project(':services:sample') {
    configurations {
        api
    }

    // New task for creating an archive
    // with the API classes.
    task apiJar(type: Jar) {
        baseName = 'api'
        dependsOn classes
        from sourceSets.main.output
        include 'sample/gradle/api/SampleService.class'
    }

    artifacts {
        // Define new apiJar as artifact.
        api apiJar
    }

    dependencies {
        compile project(':common')
    }
}

project(':web') {
    apply plugin: 'war'

    dependencies {
        // Use configuration api to only include the JAR file
        // with the API classes.
        compile project(path: ':services:sample', configuration: 'api')
        compile project(':services:sample')
        compile 'javax.servlet:servlet-api:2.5'
    }
}
